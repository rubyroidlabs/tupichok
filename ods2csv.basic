Sub ExportAllToCsv
  document = ThisComponent

  ' Use the global string tools library to generate a base filename for each CSV
  ' based on the current prefixless filename
  GlobalScope.BasicLibraries.loadLibrary("Tools")
  BaseFilename = Tools.Strings.GetFileNameWithoutExtension(document.getURL(), "/")

  ' Work out number of sheets for looping over them later.
  Sheets = document.Sheets
  NumSheets = Sheets.Count - 1

  ' Set up a propval object to store the filter properties
  Dim Propval(1) as New com.sun.star.beans.PropertyValue
  Propval(0).Name = "FilterName"
  Propval(0).Value = "Text - txt - csv (StarCalc)"
  Propval(1).Name = "FilterOptions"
  Propval(1).Value ="59,34,0,1,1"   'ASCII  59 = ;  34 = "

  For I = 0 to NumSheets
    ' For each sheet, assemble a filename and save using the filter
    SheetName = Sheets(I).Name
    document.getCurrentController.setActiveSheet(Sheets(I))
    Filename = "/tmp/" + BaseFilename + "." + SheetName + ".csv"
    FileURL = convertToURL(Filename)
    document.StoreAsURL(FileURL, Propval())
  Next I

  ' Libreoffice thinks our filename is now the last-exported-CSV filename, so close.
  Msgbox "Files saved as /tmp/" + BaseFilename + ".*.csv. You'll need to close the spreadsheet now."
End Sub
